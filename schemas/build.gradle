plugins {
    id 'java-library'
    id 'org.springframework.boot'
    id 'io.spring.dependency-management'
    id 'io.github.martinsjavacode.avro-gradle-plugin'
}

dependencies {
    api 'org.springframework.boot:spring-boot-starter-data-jpa'
    api 'org.apache.avro:avro:1.12.1'

    testImplementation platform('org.junit:junit-bom:5.10.0')
    testImplementation 'org.junit.jupiter:junit-jupiter'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

test {
    useJUnitPlatform()
}

avro {
    stringType = "String"
    sourceDir = "src/main/resources/avro"
    outputDir = "generated/java"
    fieldVisibility = "PRIVATE"
    optionalGetters = true
    validateBeforeGenerate = false
}

tasks.named('generateAvroClasses') {
    doLast {
        def outputDir = file("build/generated/java")

        if (outputDir.exists()) {
            outputDir.eachFileRecurse(groovy.io.FileType.FILES) { file ->
                if (file.name.endsWith(".java")) {
                    def content = file.text

                    def className = file.name.replace(".java", "").replaceAll("([A-Z])", "-\$1").replaceAll("^-", "").toLowerCase()

                    def injectionMarker = "extends org.apache.avro.specific.SpecificRecordBase implements org.apache.avro.specific.SpecificRecord {"
                    def fieldCode = "\n  public static final String TOPIC = \"${className}\";"

                    if (content.contains(injectionMarker) and content.contains("package org.pops.et4.jvm.project.schemas.events;")) {
                        def newContent = content.replace(
                                injectionMarker,
                                "extends org.pops.et4.jvm.project.schemas.events.KafkaEvent {" + fieldCode
                        )
                        file.write(newContent)
                    }
                }
            }
        }
    }
}

bootJar {
    enabled = false
}

jar {
    enabled = true
}

sourceSets {
    main {
        java {
            srcDir 'build/generated/java'
        }
    }
}